apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

def gitCommitCount = 100 + Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {

    signingConfigs {
        release_key {
            storeFile file("../keystore/vlottery_release_keystore.jks")
            keyAlias 'VLOTTERY_RELEASE_KEYSTORE'
            keyPassword 'htsi.release'
            storePassword 'htsi.release'
        }
        debug_key {
            storeFile file("../keystore/vlottery_debug_keystore.jks")
            keyAlias 'VLOTTERY_DEBUG_KEYSTORE'
            keyPassword 'htsi.dev'
            storePassword 'htsi.dev'
        }
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "htsi.com.vlottery"
        minSdkVersion 15
        targetSdkVersion 24

        archivesBaseName = "VLottery"
        versionName "1.0"
        versionCode gitCommitCount
        signingConfig signingConfigs.debug_key

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_key
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug_key
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.
                    replace(".apk", "_" + versionName + "_" + versionCode + "_" + getDate() + ".apk"));
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

ext {
    // Android Support AppCompat
    supportLibVersion = '24.1.1'
    // Butter Knife
    butterknifeLibVersion = '8.1.0'
    // Fonts Support
    calligraphyLibVersion = '2.2.0'
    // HTTP Connection
    retrofit2LibVersion = '2.1.0'
    okhttpLoggingInterceptorLibVersion = '3.3.1'
    // Dependence Injection
    daggerLibVersion = '2.5'
    // Annotation & Reactive
    rxAndroidLibVersion = '1.2.1'
    rxJavaLibVersion = '1.1.6'
    javaxAnnotationLibVersion = '1.0'
    // Google Play Services
    playServiceLibVersion = '9.4.0'
    // Json
    gsonLibVersion = '2.7'
    // Image
    glideLibVersion = '3.7.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    // Android Support AppCompat
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    //compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    //compile "com.android.support:cardview-v7:${supportLibVersion}"
    //compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    // Butter Knife
    compile "com.jakewharton:butterknife:${butterknifeLibVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterknifeLibVersion}"
    // Fonts Support
    compile "uk.co.chrisjenx:calligraphy:${calligraphyLibVersion}"

    // Google Sign-In
    //compile "com.google.android.gms:play-services-auth:${playServiceLibVersion}"
    // Google Firebase
    compile "com.google.firebase:firebase-database:${playServiceLibVersion}"
    compile "com.google.firebase:firebase-ads:${playServiceLibVersion}"


    // Utilities
    compile 'com.github.iwgang:countdownview:2.1'
    compile 'com.roughike:bottom-bar:1.4.0'

    // Youtube Player
    //compile files('libs/YouTubeAndroidPlayerApi.jar')

    // Facebook
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
}

apply plugin: 'com.google.gms.google-services'